// Because purging ammo that you actually want to use is annoying
// Also, hope you don't mind the coding style inconsistency

class PB_AmmoHandler : EventHandler {
    override void WorldThingSpawned(WorldEvent e) {
        let T = e.Thing;

        if (T is "HDRevolverAmmo") {
            // (Check this first, because HDRevolverAmmo inherits HDPistolAmmo)
            // .355 rounds
            let A = HDRevolverAmmo(T);
            A.itemsthatusethis.push("HD_Colt355");
        } else if (T is "HDPistolAmmo") {
            // 9mm Rounds
            let A = HDPistolAmmo(T);
            A.itemsthatusethis.push("HDMAC11");
        } else if (T is "HD9mMag30") {
            // (Check this first, because HD9mMag30 inherits HD9mMag15)
            // SMG mag.
            let A = HD9mMag30(T);
            A.itemsthatusethis.push("HDMAC11");
        } else if (T is "HD9mMag15") {
            // Pistol mag.
        } else if (T is "HDShellAmmo") {
            // Shells
            let A = HDShellAmmo(T);
            A.itemsthatusethis.push("HDUragan5");
            A.itemsthatusethis.push("HDTerminatorSG"); // BEST SHOTGUN IN THE WHOLE GAME
        } else if (T is "FourMilAmmo") {
            // 4.26 Rounds
            let A = FourMilAmmo(T);
            A.itemsthatusethis.push("HDG11Rifle");
            A.itemsthatusethis.push("HDG11Rifle_Scoped");
        } else if (T is "SevenMilAmmo") {
            let A = SevenMilAmmo(T);
            A.itemsthatusethis.push("HD_FNFAL");
        }
    }
}
